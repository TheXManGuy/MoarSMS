import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.1.4'
    id 'org.zeroturnaround.gradle.jrebel' version '1.1.5'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'net.minecrell.licenser' version '0.3'
}

group = properties.group
version = properties.version
description = properties.description

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Kotlin compiler configuration
compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = '1.8'
        apiVersion = '1.1'
        languageVersion = '1.1'
    }
}

// Licenser task
license {
    header = rootProject.file('etc/HEADER')
    ignoreFailures = false
    include "**/*.java"
    include "**/*.kt"
}

// Dependency repositories
repositories {
    mavenLocal()
    mavenCentral()

    maven {
        name = 'destroystokyo-repo'
        url = 'https://repo.destroystokyo.com/repository/maven-public/'
    }
}

// Project dependencies
dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8', version: '1.1.4'
    compileOnly(group: 'com.destroystokyo.paper', name: 'paper-api', version: paper_version) {
        exclude module: 'bungeecord-chat'
    }
    compileOnly group: 'io.netty', name: 'netty-all', version: netty_version
}

// Process plugin resources
processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [
                name: project.name,
                group: project.group,
                version: project.version,
                description: project.description
        ]
    }
}

// Wrapper task
task wrapper(type: Wrapper) {
    gradleVersion = gradle_version
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradle_version-all.zip"
}

// ShadowJar
shadowJar {
    classifier = 'shaded'
    relocate 'kotlin', 'eu.mikroskeem.moarsms.kotlin'
}

// -PuseJRebel=true
if(project.hasProperty('useJRebel') && Boolean.parseBoolean(project.getProperties().useJRebel as String)) {
    jar.dependsOn generateRebel
} else {
    // -PdontShade=true
    if(!(project.hasProperty('dontShade') && Boolean.parseBoolean(project.getProperties().dontShade as String)))
        build.dependsOn shadowJar
}

defaultTasks 'build'